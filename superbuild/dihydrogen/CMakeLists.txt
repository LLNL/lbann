enable_language(CXX)

option(DIHYDROGEN_CLONE_VIA_SSH
  "Clone DiHydrogen using SSH instead of HTTPS" ${LBANN_SB_CLONE_VIA_SSH})

if (DIHYDROGEN_CLONE_VIA_SSH)
  set(DIHYDROGEN_URL git@github.com:llnl/DiHydrogen.git
    CACHE STRING "The URL from which to clone DiHydrogen")
else ()
  set(DIHYDROGEN_URL https://github.com/llnl/DiHydrogen
    CACHE STRING "The URL from which to clone DiHydrogen")
endif ()

set(DIHYDROGEN_TAG "develop"
  CACHE STRING "The git tag to checkout for DiHydrogen")

set(DIHYDROGEN_CMAKE_BUILD_TYPE "${CMAKE_BUILD_TYPE}"
  CACHE STRING "The build type for DiHydrogen.")

# Where to install DiHydrogen
set(DIHYDROGEN_CMAKE_INSTALL_PREFIX "${CMAKE_INSTALL_PREFIX}"
  CACHE PATH "The installation location of DiHydrogen.")

# Aluminum
set(LBANN_SB_FWD_DIHYDROGEN_Aluminum_DIR
  "${ALUMINUM_DIR}/lib64/cmake/aluminum"
  CACHE STRING "The path to Aluminum for DiHydrogen.")
list(APPEND _DIHYDROGEN_DEPENDS ALUMINUM)

option(DIHYDROGEN_ENABLE_DISTCONV_LEGACY "Whether to enable Distconv in DiHydrogen" OFF)
if (DIHYDROGEN_ENABLE_DISTCONV_LEGACY)
  set(LBANN_SB_FWD_DIHYDROGEN_H2_ENABLE_CUDA ON)
  set(LBANN_SB_FWD_DIHYDROGEN_H2_ENABLE_DISTCONV_LEGACY ON)
  # CUB
  set(LBANN_SB_FWD_DIHYDROGEN_CUB_DIR "${CUB_DIR}")
  list(APPEND _DIHYDROGEN_DEPENDS CUB)
else ()
  set(LBANN_SB_FWD_DIHYDROGEN_H2_ENABLE_DISTCONV_LEGACY OFF)
endif ()

# Get the list of DiHydrogen variables
get_property(DIHYDROGEN_VARIABLES DIRECTORY PROPERTY VARIABLES)
list(FILTER DIHYDROGEN_VARIABLES INCLUDE REGEX
  "^DIHYDROGEN_.*\|^LBANN_SB_FWD_DIHYDROGEN_.*")
list(FILTER DIHYDROGEN_VARIABLES EXCLUDE REGEX "DIHYDROGEN_URL\|DIHYDROGEN_TAG")

create_cmake_arguments(
  OUTPUT_VARIABLE DIHYDROGEN_CMAKE_ARGS
  PACKAGE_NAME DIHYDROGEN
  SKIP_VARS_WITH_PREFIXES "LBANN_SB"
  EXTRA_REMOVE_PREFIXES "LBANN_SB_FWD_DIHYDROGEN"
  VARIABLES ${DIHYDROGEN_VARIABLES})

message(STATUS ${DIHYDROGEN_VARIABLES})
message(STATUS ${DIHYDROGEN_CMAKE_ARGS})

include(ExternalProject)
ExternalProject_Add(DIHYDROGEN
  PREFIX ${CMAKE_CURRENT_BINARY_DIR}
  TMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/tmp
  STAMP_DIR ${CMAKE_CURRENT_BINARY_DIR}/stamp
  GIT_REPOSITORY ${DIHYDROGEN_URL}
  GIT_TAG ${DIHYDROGEN_TAG}
  DEPENDS ${_DIHYDROGEN_DEPENDS}
  SOURCE_DIR ${CMAKE_CURRENT_BINARY_DIR}/src
  BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/build
  INSTALL_DIR ${DIHYDROGEN_CMAKE_INSTALL_PREFIX}
  USES_TERMINAL_BUILD 1
  LOG_DOWNLOAD 1
  LOG_UPDATE 1
  LOG_CONFIGURE 1
  LOG_BUILD 1
  LOG_INSTALL 1
  LOG_TEST 1
  LIST_SEPARATOR |
  CMAKE_ARGS
  -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
  -DCMAKE_CXX_FLAGS=${CMAKE_CXX_FLAGS}
  -DCMAKE_CUDA_COMPILER=${CMAKE_CUDA_COMPILER}
  -DCMAKE_CUDA_FLAGS=${CMAKE_CUDA_FLAGS}
  -DCMAKE_CUDA_HOST_COMPILER=${CMAKE_CUDA_HOST_COMPILER}
  ${DIHYDROGEN_CMAKE_ARGS}
  )

set(DIHYDROGEN_DIR ${DIHYDROGEN_CMAKE_INSTALL_PREFIX}
  CACHE INTERNAL "The install prefix of DiHydrogen.")
